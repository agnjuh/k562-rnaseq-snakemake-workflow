import pandas as pd

configfile: "config.yaml"

SAMPLES_TSV = "samples.tsv"
samples = pd.read_csv(SAMPLES_TSV, sep="\t")

SAMPLE_NAMES = samples["sample"].tolist()
R1 = dict(zip(samples["sample"], samples["r1"]))
R2 = dict(zip(samples["sample"], samples["r2"]))

THREADS = int(config["params"]["threads"])


rule all:
    input:
        # FastQC raw
        expand("results/qc/fastqc/raw/{sample}_R1_fastqc.html", sample=SAMPLE_NAMES),
        expand("results/qc/fastqc/raw/{sample}_R1_fastqc.zip",  sample=SAMPLE_NAMES),
        expand("results/qc/fastqc/raw/{sample}_R2_fastqc.html", sample=SAMPLE_NAMES),
        expand("results/qc/fastqc/raw/{sample}_R2_fastqc.zip",  sample=SAMPLE_NAMES),

        # fastp
        expand("results/trimmed/{sample}_R1.trim.fastq.gz", sample=SAMPLE_NAMES),
        expand("results/trimmed/{sample}_R2.trim.fastq.gz", sample=SAMPLE_NAMES),
        expand("results/trimmed/{sample}.fastp.html",       sample=SAMPLE_NAMES),
        expand("results/trimmed/{sample}.fastp.json",       sample=SAMPLE_NAMES),

        # FastQC trimmed
        expand("results/qc/fastqc/trimmed/{sample}_R1.trim_fastqc.html", sample=SAMPLE_NAMES),
        expand("results/qc/fastqc/trimmed/{sample}_R1.trim_fastqc.zip",  sample=SAMPLE_NAMES),
        expand("results/qc/fastqc/trimmed/{sample}_R2.trim_fastqc.html", sample=SAMPLE_NAMES),
        expand("results/qc/fastqc/trimmed/{sample}_R2.trim_fastqc.zip",  sample=SAMPLE_NAMES),

        # MultiQC
        "results/qc/multiqc/multiqc_report.html",

        # HISAT2 index
        expand("data/reference/chm13v2/hisat2_index/chm13v2.{idx}.ht2", idx=range(1, 9)),

        # BAM + BAI
        expand("results/align/{sample}.sorted.bam", sample=SAMPLE_NAMES),
        expand("results/align/{sample}.sorted.bam.bai", sample=SAMPLE_NAMES),


rule fastqc_raw:
    input:
        r1=lambda wc: R1[wc.sample],
        r2=lambda wc: R2[wc.sample]
    output:
        r1_html="results/qc/fastqc/raw/{sample}_R1_fastqc.html",
        r1_zip="results/qc/fastqc/raw/{sample}_R1_fastqc.zip",
        r2_html="results/qc/fastqc/raw/{sample}_R2_fastqc.html",
        r2_zip="results/qc/fastqc/raw/{sample}_R2_fastqc.zip"
    threads: 2
    conda:
        "envs/fastqc.yaml"
    shell:
        r"""
        mkdir -p results/qc/fastqc/raw
        fastqc -t {threads} -o results/qc/fastqc/raw {input.r1} {input.r2}
        """


rule fastp_trim:
    input:
        r1=lambda wc: R1[wc.sample],
        r2=lambda wc: R2[wc.sample]
    output:
        r1="results/trimmed/{sample}_R1.trim.fastq.gz",
        r2="results/trimmed/{sample}_R2.trim.fastq.gz",
        html="results/trimmed/{sample}.fastp.html",
        json="results/trimmed/{sample}.fastp.json"
    threads: THREADS
    conda:
        "envs/fastp.yaml"
    params:
        extra=config["params"]["fastp"]["extra"]
    shell:
        r"""
        mkdir -p results/trimmed
        fastp \
          -w {threads} \
          -i {input.r1} -I {input.r2} \
          -o {output.r1} -O {output.r2} \
          -h {output.html} -j {output.json} \
          {params.extra}
        """


rule fastqc_trimmed:
    input:
        r1="results/trimmed/{sample}_R1.trim.fastq.gz",
        r2="results/trimmed/{sample}_R2.trim.fastq.gz"
    output:
        r1_html="results/qc/fastqc/trimmed/{sample}_R1.trim_fastqc.html",
        r1_zip="results/qc/fastqc/trimmed/{sample}_R1.trim_fastqc.zip",
        r2_html="results/qc/fastqc/trimmed/{sample}_R2.trim_fastqc.html",
        r2_zip="results/qc/fastqc/trimmed/{sample}_R2.trim_fastqc.zip"
    threads: 2
    conda:
        "envs/fastqc.yaml"
    shell:
        r"""
        mkdir -p results/qc/fastqc/trimmed
        fastqc -t {threads} -o results/qc/fastqc/trimmed {input.r1} {input.r2}
        """


rule multiqc:
    input:
        raw_fastqc_html=(
            expand("results/qc/fastqc/raw/{sample}_R1_fastqc.html", sample=SAMPLE_NAMES) +
            expand("results/qc/fastqc/raw/{sample}_R2_fastqc.html", sample=SAMPLE_NAMES)
        ),
        trimmed_fastqc_html=(
            expand("results/qc/fastqc/trimmed/{sample}_R1.trim_fastqc.html", sample=SAMPLE_NAMES) +
            expand("results/qc/fastqc/trimmed/{sample}_R2.trim_fastqc.html", sample=SAMPLE_NAMES)
        ),
        fastp_html=expand("results/trimmed/{sample}.fastp.html", sample=SAMPLE_NAMES)
    output:
        html="results/qc/multiqc/multiqc_report.html"
    threads: 1
    conda:
        "envs/multiqc.yaml"
    shell:
        r"""
        mkdir -p results/qc/multiqc
        multiqc -o results/qc/multiqc results/qc/fastqc/raw results/qc/fastqc/trimmed results/trimmed
        """


rule hisat2_index:
    input:
        fasta_gz="data/reference/chm13v2/chm13v2.0.fa.gz"
    output:
        expand("data/reference/chm13v2/hisat2_index/chm13v2.{idx}.ht2", idx=range(1, 9))
    threads: 4
    conda:
        "envs/hisat2.yaml"
    shell:
        r"""
        mkdir -p data/reference/chm13v2/hisat2_index
        gunzip -c {input.fasta_gz} > data/reference/chm13v2/chm13v2.0.fa
        hisat2-build -p {threads} data/reference/chm13v2/chm13v2.0.fa data/reference/chm13v2/hisat2_index/chm13v2
        rm -f data/reference/chm13v2/chm13v2.0.fa
        """


rule hisat2_align:
    input:
        idx=expand("data/reference/chm13v2/hisat2_index/chm13v2.{i}.ht2", i=range(1, 9)),
        r1="results/trimmed/{sample}_R1.trim.fastq.gz",
        r2="results/trimmed/{sample}_R2.trim.fastq.gz"
    output:
        bam=temp("results/align/{sample}.unsorted.bam")
    threads: 4
    conda:
        "envs/hisat2.yaml"
    shell:
        r"""
        mkdir -p results/align
        hisat2 -p {threads} -x data/reference/chm13v2/hisat2_index/chm13v2 \
          -1 {input.r1} -2 {input.r2} \
        | samtools view -@ 2 -bS - \
        > {output.bam}
        """


rule sort_bam:
    input:
        "results/align/{sample}.unsorted.bam"
    output:
        bam="results/align/{sample}.sorted.bam"
    threads: 4
    resources:
        tmpdir="/tmp"
    conda:
        "envs/hisat2.yaml"
    shell:
        r"""
        mkdir -p {resources.tmpdir}
        samtools sort \
          -@ {threads} \
          -m 1G \
          -T {resources.tmpdir}/{wildcards.sample}.sorttmp \
          -o {output.bam} \
          {input}
        """


rule index_bam:
    input:
        "results/align/{sample}.sorted.bam"
    output:
        bai="results/align/{sample}.sorted.bam.bai"
    threads: 1
    conda:
        "envs/hisat2.yaml"
    shell:
        r"""
        samtools index {input}
        """
